algoritmo "CorrigindoProvas"
var
   gab: vetor[1..5] de Caractere
   prova: vetor[1..5] de Caractere
   nome: vetor[1..3] de Caractere
   nota: vetor[1..3] de Real
   a: Inteiro
   s, m: Real
   
Procedimento CadastroGab ()
Var
   c: Inteiro
Inicio
      EscrevaL("Passo 1 - Cadastro de Gabaritos")
      EscrevaL("-------------------------------")
      Para c <- 1 ate 5 faca
           Escreva("Questão ",c,": ")
           Leia(gab[c])
      FimPara
FimProcedimento

Funcao CadastraProva(): Real
Var
   c: Inteiro
   notaFinal: Real
   
Inicio
      notaFinal <- 0
      EscrevaL("Respostas Dadas ")
      Para c <- 1 ate 5 faca
           Escreva("Quetão ",c,": ")
           Leia(prova[c])
           Se(maiusc(prova[c]) = maiusc(gab[c])) entao
             notaFinal <- notaFinal + 2
           FimSe
      FimPara
      Retorne notaFinal
FimFuncao

inicio
      CadastroGab ()
      
      Para a <- 1 ate 3 faca
           LimpaTela
           EscrevaL("--------")
           EscrevaL("Aluna ",a)
           EscrevaL("--------")
           Escreva("Nome: ")
           Leia(nome[a])
           nota[a] <- CadastraProva()
           s <- s + nota[a]
      FimPara
      LimpaTela
      EscrevaL("------------")
      EscrevaL("NOTAS FINAIS")
      EscrevaL("------------")
      Para a <- 1 ate 3 faca
           EscrevaL(nome[a]:10, nota[a]:4:1)
      FimPara
      m <- s / 3
      EscrevaL("----------------------")
      EscrevaL("Media da Turma:", m:4:1)
fimalgoritmo
